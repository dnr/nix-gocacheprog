(final: prev: {
  # This is only required for Go < 1.24, it will be enabled by default after that.
  go_1_21_cacheprog = prev.go_1_21.overrideAttrs { GOEXPERIMENT = "cacheprog"; doCheck = false; };
  go_1_22_cacheprog = prev.go_1_22.overrideAttrs { GOEXPERIMENT = "cacheprog"; doCheck = false; };
  go_1_23_cacheprog = prev.go_1_23.overrideAttrs { GOEXPERIMENT = "cacheprog"; doCheck = false; };

  nixGocacheprogHook = let const = import ./const.nix; in final.makeSetupHook {
    name = "nix-gocacheprog-hook";
  } (final.writeScript "nix-gocacheprog-hook.sh" ''
    _nixGocacheprogHook() {
      local client=${const.SandboxCacheDir}/client
      if [[ ! -x $client ]]; then
        echo "gocacheprog client not found, skipping GOCACHEPROG"
        return
      fi

      export GOCACHEPROG=$client
      echo "Setting GOCACHEPROG to $GOCACHEPROG"

      # default value from https://go.dev/ref/mod
      case "''${GOPROXY:=https://proxy.golang.org,direct}" in
        off|file*) # main build, do nothing
          ;;
        *) # module build
          $client -mode goproxy &
          export GOPROXY="http://localhost${const.ProxyListen},$GOPROXY"
          echo "Using nix-gocacheprog module proxy"
          echo "Setting GOPROXY to $GOPROXY"
          ;;
      esac
    }
    postConfigureHooks+=(_nixGocacheprogHook)
  '');

  # buildGo*Module is generated by a function that's not overridable (I think?), so we have to
  # import it again to override the "go" attribute. Then we add our hook to nativeBuildInputs.
  buildGo121ModuleCached = args: final.callPackage <nixpkgs/pkgs/build-support/go/module.nix> {
    go = final.go_1_21_cacheprog;
  } (args // { nativeBuildInputs = (args.nativeBuildInputs or []) ++ [ final.nixGocacheprogHook ]; });

  buildGo122ModuleCached = args: final.callPackage <nixpkgs/pkgs/build-support/go/module.nix> {
    go = final.go_1_22_cacheprog;
  } (args // { nativeBuildInputs = (args.nativeBuildInputs or []) ++ [ final.nixGocacheprogHook ]; });

  buildGo123ModuleCached = args: final.callPackage <nixpkgs/pkgs/build-support/go/module.nix> {
    go = final.go_1_23_cacheprog;
  } (args // { nativeBuildInputs = (args.nativeBuildInputs or []) ++ [ final.nixGocacheprogHook ]; });

  # 1.24 and above don't need a special package.
  buildGo124ModuleCached = args: prev.buildGo124Module (
    args // { nativeBuildInputs = (args.nativeBuildInputs or []) ++ [ final.nixGocacheprogHook ]; });
})
